CMAKE_MINIMUM_REQUIRED(VERSION 3.13)
CMAKE_POLICY(VERSION 3.13)


SET (PRIVATE_INCLUDE_DIRS CACHE INTERNAL "The list of private include folders for the network module.")
SET (MODULE_LIBS CACHE INTERNAL "The list of libraries that the network module should link against.")


LIST (APPEND MODULE_LIBS gmCore)

# Boost ASIO (headers only)
FIND_PACKAGE (ASIO)
IF (ASIO_FOUND)
  SET(HAVE_ASIO 1)
  OPTION(gramods_ENABLE_ASIO "Enable functionality that requires ASIO" ON)
  IF (NOT gramods_ENABLE_ASIO)
    MESSAGE (STATUS "Not including gmNetwork - requires ASIO (disabled)")
    RETURN()
  ENDIF()
ELSE()
  MESSAGE (STATUS "Not including gmNetwork - requires ASIO")
  RETURN()
ENDIF()
LIST (APPEND PRIVATE_INCLUDE_DIRS ${ASIO_INCLUDE_DIRS})


OPTION(gramods_ACTIVATE_ASIO_HANDLER_TRACKING
  "Activate ASIO handler tracking and debug output to the standard error stream."
  OFF)

INCLUDE(WinVer)
get_WIN32_WINNT(_WIN32_WINNT)
SET(_WIN32_WINNT "${_WIN32_WINNT}")

CONFIGURE_FILE(
  ${CMAKE_CURRENT_SOURCE_DIR}/src/config_cmake.hh
  ${CMAKE_CURRENT_BINARY_DIR}/include/gmNetwork/config.hh)


INCLUDE(GetSourcesAndHeaders)
get_sources_and_headers()

SOURCE_GROUP("headers" FILES ${HEADERS_ABSOLUTE})
SOURCE_GROUP("sources" FILES ${SOURCES})

ADD_LIBRARY(gmNetwork SHARED)

TARGET_INCLUDE_DIRECTORIES(gmNetwork
  PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>
  $<INSTALL_INTERFACE:include>
  PRIVATE
  ${PRIVATE_INCLUDE_DIRS})
TARGET_SOURCES(gmNetwork
  PRIVATE ${SOURCES}
  PUBLIC ${HEADERS_MAGIC}
  )
TARGET_LINK_LIBRARIES(gmNetwork ${MODULE_LIBS})

SET_PROPERTY(TARGET gmNetwork PROPERTY CXX_STANDARD 14)
SET_PROPERTY(TARGET gmNetwork PROPERTY WINDOWS_EXPORT_ALL_SYMBOLS TRUE)
SET_PROPERTY(TARGET gmNetwork PROPERTY PUBLIC_HEADER ${HEADERS_MAGIC})

INSTALL(TARGETS gmNetwork
  EXPORT Gramods-gmNetwork-targets
  ARCHIVE LIBRARY RUNTIME
  PUBLIC_HEADER DESTINATION include/gmNetwork)
INSTALL(EXPORT Gramods-gmNetwork-targets
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/gramods/cmake)

SET(gramods_LIB_TARGET_FILES ${gramods_LIB_TARGET_FILES} "$<TARGET_FILE:gmNetwork>" PARENT_SCOPE)
