# Final resort if no "real" sdl2-config.cmake file is available

FIND_PATH(SDL2_INCLUDE_DIR SDL.h
 HINTS $ENV{SDL2DIR} PATH_SUFFIXES include/SDL2 include
  DOC "Path to SDL2 include folder")

IF (NOT WIN32)

  FIND_LIBRARY(SDL2_LIBRARY NAMES SDL2
    HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
    DOC "Path to SDL2 shared library")
  FIND_LIBRARY(SDL2MAIN_LIBRARY NAMES SDL2main
   HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
   DOC "Path to SDL2main shared library")

  ADD_LIBRARY(SDL2::SDL2 SHARED IMPORTED)
  IF (SDL2_INCLUDE_DIR AND SDL2_LIBRARY)
    SET_TARGET_PROPERTIES(SDL2::SDL2 PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES ${SDL2_INCLUDE_DIR}
      IMPORTED_LOCATION ${SDL2_LIBRARY})
  ELSE()
    MESSAGE(SEND_ERROR "Could not set properties of target SDL2::SDL2 - set SDL2_INCLUDE_DIR and SDL2_LIBRARY or unset SDL2_DIR")
  ENDIF()

  ADD_LIBRARY(SDL2::SDL2main STATIC IMPORTED)
  IF (SDL2MAIN_LIBRARY)
    SET_TARGET_PROPERTIES(SDL2::SDL2main PROPERTIES
      IMPORTED_LOCATION ${SDL2MAIN_LIBRARY})
  ELSE()
    MESSAGE(SEND_ERROR "Could not set properties of target SDL2::SDL2main - set SDL2MAIN_LIBRARY or unset SDL2_DIR")
  ENDIF()

ELSE()

  FIND_FILE(SDL2_LIBRARY NAMES SDL2.dll
    HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
    DOC "Path to shared library for SDL2 (DLL on Windows)")
  FIND_LIBRARY(SDL2_IMPLIB NAMES SDL2
    HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
    DOC "Path to library file for SDL2 (.lib file)")
  FIND_FILE(SDL2MAIN_LIBRARY NAMES SDL2main.dll
    HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
    DOC "Path to shared library for SDL2main (DLL on Windows)")
  FIND_LIBRARY(SDL2MAIN_IMPLIB NAMES SDL2main
    HINTS $ENV{SDL2DIR} PATH_SUFFIXES lib64 lib
    DOC "Path to library file for SDL2main (.lib file)")

  ADD_LIBRARY(SDL2::SDL2 SHARED IMPORTED)
  IF (SDL2_INCLUDE_DIR AND SDL2_LIBRARY AND SDL2_IMPLIB)
      SET_TARGET_PROPERTIES(SDL2::SDL2 PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES ${SDL2_INCLUDE_DIR}
        IMPORTED_LOCATION ${SDL2_LIBRARY}
        IMPORTED_IMPLIB ${SDL2_IMPLIB})
  ELSE()
    MESSAGE(SEND_ERROR "Could not set properties of target SDL2::SDL2 - set SDL2_* or unset SDL2_DIR")
  ENDIF()

  ADD_LIBRARY(SDL2::SDL2main STATIC IMPORTED)
  IF (SDL2MAIN_LIBRARY AND SDL2MAIN_IMPLIB)
    SET_TARGET_PROPERTIES(SDL2::SDL2main PROPERTIES
      IMPORTED_LOCATION ${SDL2MAIN_LIBRARY}
      IMPORTED_IMPLIB ${SDL2MAIN_IMPLIB})
  ELSE()
    MESSAGE(SEND_ERROR "Could not set properties of target SDL2::SDL2main - set SDL2MAIN_* or unset SDL2_DIR")
  ENDIF()

ENDIF()
